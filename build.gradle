plugins {
    id 'java'
    id 'io.freefair.lombok' version '9.1.0'
    id "com.gradleup.shadow" version '9.3.0'
}

group = 'no.vestlandetmc'
version = '3.1.0'

repositories {
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://repo.glaremasters.me/repository/bloodshot/' }
    maven { url = 'https://maven.enginehub.org/repo/' }
    maven { url = 'https://repo.essentialsx.net/releases/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT'
    compileOnly 'com.github.GriefPrevention:GriefPrevention:16.18.5'
    compileOnly 'com.griefdefender:api:2.1.1-SNAPSHOT'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.14'
    compileOnly 'com.github.LeonMangler:PremiumVanishAPI:2.9.0-4'
    compileOnly 'net.essentialsx:EssentialsX:2.21.2'
    compileOnly 'com.github.Zrips:CMI-API:9.7.14.3'

    implementation 'org.bstats:bstats-bukkit:3.1.0'
}

shadowJar {
    relocate 'org.bstats', 'no.vestlandetmc.elevator.bstats'
    archiveFileName.set('Elevator.jar')
}

jar.dependsOn shadowJar

jar {
    enabled = false
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
